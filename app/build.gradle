apply plugin: 'com.android.application'

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.neil.hellogradle"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    // Note: signingConfigs must be placed ahead of buildTypes so
    // that signingConfig signingConfigs.release in buildTypes can
    // be properly referenced.
    signingConfigs {
        release {
            // Load keystore properties.
            def keyStoreFileLocation = rootProject.file("certificate/keystore.properties");
            def keyStoreProperties = new Properties();
            keyStoreProperties.load(new FileInputStream(keyStoreFileLocation));

            // Assign signing properties.
            keyAlias keyStoreProperties['keyAlias']
            storeFile file(keyStoreProperties['storeFile'])
            storePassword keyStoreProperties['storePassword']
            keyPassword keyStoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }


    productFlavors {
        development {
            buildConfigField "boolean", "enableLog", "true"
        }

        integration {
            buildConfigField "boolean", "enableLog", "true"
        }

        qa {
            applicationId "com.neil.hellogradle.qa"
            buildConfigField "boolean", "enableLog", "true"
        }

        production {
            buildConfigField "boolean", "enableLog", "false"
        }

    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.1.1'
}
